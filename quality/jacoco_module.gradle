apply plugin: 'jacoco'
apply from: '../quality/jacoco_vars.gradle'
apply from: '../config/dependencies.gradle'

jacoco {
    toolVersion = jacocoToolsVersion
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group taskGroup
    description taskDescription

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/kotlin"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec',
            'outputs/code-coverage/connected/*coverage.ec'
    ])
}
